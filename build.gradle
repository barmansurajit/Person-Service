plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'

group = 'com.triton'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

sourceSets {
	itest
}

idea {
	module {
		testSourceDirs += project.sourceSets.itest.java.srcDirs
		testSourceDirs += project.sourceSets.itest.resources.srcDirs
		scopes.TEST.plus += [configurations.itestCompileClasspath]
	}
}

configurations {
	itestImplementation.extendsFrom implementation
	itestRuntime.extendsFrom runtime
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	compileOnly 'org.projectlombok:lombok'

	runtimeOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'

	itestImplementation sourceSets.main.output
}

task itest(type: Test, group: 'Verfication'){
	testClassesDirs = sourceSets.itest.output.classesDirs
	classpath = sourceSets.itest.runtimeClasspath
	systemProperties['jar.path'] = jar.archivePath
}

tasks.check.dependsOn itest